plugins {
	id 'org.jetbrains.kotlin.jvm'
	id 'application'
	id 'maven'
}

version = projectVersion

mainClassName = 'fwcd.ktda.KDAMainKt'
description = 'Debug Adapter for Kotlin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

startScripts {
	applicationName = "kotlin-debug-adapter"
}

dependencies {
	// The JSON-RPC and Debug Adapter Protocol implementations
	implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j.debug:0.6.0'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation('com.github.fwcd:kotlin-language-server-shared:0.2.9') {
		
	}
	// The Java Debug Interface classes (com.sun.jdi.*)
	implementation files("${System.properties['java.home']}/../lib/tools.jar")
	testImplementation 'junit:junit:4.12'
}

task fixFilePermissions(type: Exec) {
	// When running on macOS or Linux the start script
	// needs executable permissions to run.
	onlyIf { !System.getProperty('os.name').toLowerCase().contains('windows') }
	commandLine 'chmod', '+x', "${installDist.destinationDir}/bin/kotlin-debug-adapter"
}

test {
	testLogging {
		events 'failed'
		exceptionFormat 'short'
	}
}

run {
	standardInput = System.in
}

distZip {
	archiveName = "${project.name}.zip"
}

installDist.finalizedBy fixFilePermissions
